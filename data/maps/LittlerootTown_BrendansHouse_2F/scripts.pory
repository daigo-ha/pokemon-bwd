raw `
LittlerootTown_BrendansHouse_2F_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, LittlerootTown_BrendansHouse_2F_OnTransition
	map_script MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE, LittlerootTown_BrendansHouse_2F_OnWarp
	.byte 0

LittlerootTown_BrendansHouse_2F_OnTransition:
	call_if_lt VAR_LITTLEROOT_RIVAL_STATE, 2, LittlerootTown_BrendansHouse_2F_EventScript_CheckSetReadyToMeetBrendan
	call_if_ge VAR_LITTLEROOT_RIVAL_STATE, 3, LittlerootTown_BrendansHouse_2F_EventScript_CheckShouldUpdateBrendanPos
	call_if_eq VAR_LITTLEROOT_INTRO_STATE, 4, PlayersHouse_2F_EventScript_BlockStairsUntilClockIsSet
	call SecretBase_EventScript_SetDecorationFlags
	setvar VAR_SECRET_BASE_INITIALIZED, 0
	call_if_unset FLAG_STARTER_BAG, MCRoom_EventScript_StarterBag
	removeobject LOCALID_MC2F_BIANCA
	setflag FLAG_MC2F_BIANCA
	return

LittlerootTown_BrendansHouse_2F_EventScript_CheckShouldUpdateBrendanPos::
	goto_if_set FLAG_MET_RIVAL_LILYCOVE, LittlerootTown_BrendansHouse_2F_EventScript_TryUpdateBrendanPos
	goto_if_ge VAR_BIRCH_LAB_STATE, 2, LittlerootTown_BrendansHouse_2F_EventScript_Ret
	goto LittlerootTown_BrendansHouse_2F_EventScript_TryUpdateBrendanPos

LittlerootTown_BrendansHouse_2F_EventScript_TryUpdateBrendanPos::
	checkplayergender
	goto_if_eq VAR_RESULT, MALE, LittlerootTown_BrendansHouse_2F_EventScript_Ret
	@ Odd that the MaysHouse equivalent was used below instead
	goto_if_ge VAR_DEX_UPGRADE_JOHTO_STARTER_STATE, 2, LittlerootTown_MaysHouse_2F_EventScript_Ret
	setobjectxyperm LOCALID_RIVALS_HOUSE_2F_RIVAL, 0, 2
	setobjectmovementtype LOCALID_RIVALS_HOUSE_2F_RIVAL, MOVEMENT_TYPE_FACE_UP
	return

LittlerootTown_BrendansHouse_2F_EventScript_Ret::
	return

LittlerootTown_BrendansHouse_2F_EventScript_CheckSetReadyToMeetBrendan::
	checkplayergender
	goto_if_eq VAR_RESULT, FEMALE, LittlerootTown_BrendansHouse_2F_EventScript_SetReadyToMeetBrendan
	return

LittlerootTown_BrendansHouse_2F_EventScript_SetReadyToMeetBrendan::
	setvar VAR_LITTLEROOT_RIVAL_STATE, 2
	return

LittlerootTown_BrendansHouse_2F_OnWarp:
	map_script_2 VAR_SECRET_BASE_INITIALIZED, 0, LittlerootTown_BrendansHouse_2F_EventScript_CheckInitDecor
	.2byte 0

LittlerootTown_BrendansHouse_2F_EventScript_CheckInitDecor::
	checkplayergender
	goto_if_eq VAR_RESULT, MALE, SecretBase_EventScript_InitDecorations
	end

LittlerootTown_BrendansHouse_2F_EventScript_RivalsPokeBall::
	lockall
	goto_if_eq VAR_LITTLEROOT_RIVAL_STATE, 2, LittlerootTown_BrendansHouse_2F_EventScript_MeetBrendan
	msgbox RivalsHouse_2F_Text_ItsRivalsPokeBall, MSGBOX_DEFAULT
	releaseall
	end

LittlerootTown_BrendansHouse_2F_EventScript_MeetBrendan::
	delay 10
	addobject LOCALID_RIVALS_HOUSE_2F_RIVAL
	applymovement LOCALID_RIVALS_HOUSE_2F_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanEnters
	waitmovement 0
	playse SE_PIN
	applymovement LOCALID_RIVALS_HOUSE_2F_RIVAL, Common_Movement_ExclamationMark
	waitmovement 0
	applymovement LOCALID_RIVALS_HOUSE_2F_RIVAL, Common_Movement_Delay48
	waitmovement 0
	delay 10
	playbgm MUS_ENCOUNTER_BRENDAN, TRUE
	call_if_eq VAR_FACING, DIR_NORTH, LittlerootTown_BrendansHouse_2F_EventScript_MeetBrendanNorth
	call_if_eq VAR_FACING, DIR_SOUTH, LittlerootTown_BrendansHouse_2F_EventScript_MeetBrendanSouth
	call_if_eq VAR_FACING, DIR_WEST, LittlerootTown_BrendansHouse_2F_EventScript_MeetBrendanWest
	call_if_eq VAR_FACING, DIR_EAST, LittlerootTown_BrendansHouse_2F_EventScript_MeetBrendanEast
	setvar VAR_LITTLEROOT_RIVAL_STATE, 3
	setflag FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F_POKE_BALL
	clearflag FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_RIVAL_BEDROOM
	setvar VAR_LITTLEROOT_TOWN_STATE, 1
	savebgm MUS_DUMMY
	fadedefaultbgm
	releaseall
	end

LittlerootTown_BrendansHouse_2F_EventScript_MeetBrendanNorth::
	applymovement LOCALID_RIVALS_HOUSE_2F_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerNorth
	waitmovement 0
	applymovement LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterRight
	waitmovement 0
	msgbox RivalsHouse_2F_Text_BrendanWhoAreYou, MSGBOX_DEFAULT
	closemessage
	applymovement LOCALID_PLAYER, LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanNorth
	applymovement LOCALID_RIVALS_HOUSE_2F_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCNorth
	waitmovement 0
	return

LittlerootTown_BrendansHouse_2F_EventScript_MeetBrendanSouth::
	applymovement LOCALID_RIVALS_HOUSE_2F_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerSouth
	waitmovement 0
	applymovement LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterRight
	waitmovement 0
	msgbox RivalsHouse_2F_Text_BrendanWhoAreYou, MSGBOX_DEFAULT
	closemessage
	applymovement LOCALID_PLAYER, LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanSouth
	applymovement LOCALID_RIVALS_HOUSE_2F_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCSouth
	waitmovement 0
	return

LittlerootTown_BrendansHouse_2F_EventScript_MeetBrendanWest::
	applymovement LOCALID_RIVALS_HOUSE_2F_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerWest
	waitmovement 0
	applymovement LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterRight
	waitmovement 0
	msgbox RivalsHouse_2F_Text_BrendanWhoAreYou, MSGBOX_DEFAULT
	closemessage
	applymovement LOCALID_PLAYER, LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanWest
	applymovement LOCALID_RIVALS_HOUSE_2F_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCWest
	waitmovement 0
	return

LittlerootTown_BrendansHouse_2F_EventScript_MeetBrendanEast::
	applymovement LOCALID_RIVALS_HOUSE_2F_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerEast
	waitmovement 0
	applymovement LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterUp
	waitmovement 0
	msgbox RivalsHouse_2F_Text_BrendanWhoAreYou, MSGBOX_DEFAULT
	closemessage
	applymovement LOCALID_RIVALS_HOUSE_2F_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCEast
	waitmovement 0
	return

LittlerootTown_BrendansHouse_2F_Movement_BrendanEnters:
	walk_down
	walk_down
	walk_in_place_faster_left
	step_end

LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerNorth:
	walk_left
	walk_left
	walk_down
	walk_down
	walk_left
	step_end

LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCNorth:
	walk_up
	walk_up
	walk_up
	walk_left
	walk_left
	walk_left
	walk_left
	walk_in_place_faster_up
	step_end

LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanNorth:
	delay_16
	walk_in_place_faster_up
	delay_16
	delay_16
	delay_16
	delay_16
	walk_in_place_faster_left
	step_end

LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerSouth:
	walk_left
	walk_left
	walk_left
	step_end

LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCSouth:
	walk_up
	walk_left
	walk_left
	walk_left
	walk_left
	walk_in_place_faster_up
	step_end

LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanSouth:
	delay_16
	walk_in_place_faster_up
	delay_16
	delay_16
	walk_in_place_faster_left
	step_end

LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerWest:
	walk_left
	walk_left
	walk_down
	walk_in_place_faster_left
	step_end

LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCWest:
	walk_up
	walk_up
	walk_left
	walk_left
	walk_left
	walk_left
	walk_left
	walk_in_place_faster_up
	step_end

LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanWest:
	delay_8
	delay_16
	walk_in_place_faster_up
	delay_16
	delay_16
	walk_in_place_faster_left
	step_end

LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerEast:
	walk_left
	walk_left
	walk_left
	walk_left
	walk_left
	walk_in_place_faster_down
	step_end

LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCEast:
	walk_up
	walk_left
	walk_left
	walk_in_place_faster_up
	step_end

@ Unused, the player is already facing this direction so its unneeded
LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanEast:
	delay_16
	delay_16
	walk_in_place_faster_left
	step_end

LittlerootTown_BrendansHouse_2F_EventScript_PC::
	lockall
	checkplayergender
	goto_if_eq VAR_RESULT, MALE, LittlerootTown_BrendansHouse_2F_EventScript_CheckPlayersPC
	goto_if_eq VAR_RESULT, FEMALE, LittlerootTown_BrendansHouse_2F_EventScript_CheckRivalsPC
	end

LittlerootTown_BrendansHouse_2F_EventScript_CheckPlayersPC::
	setvar VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE
	special DoPCTurnOnEffect
	playse SE_PC_ON
	msgbox gText_PlayerHouseBootPC, MSGBOX_DEFAULT
	special BedroomPC
	waitstate
	releaseall
	end

LittlerootTown_BrendansHouse_2F_EventScript_TurnOffPlayerPC::
	setvar VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE
	playse SE_PC_OFF
	special DoPCTurnOffEffect
	releaseall
	end

LittlerootTown_BrendansHouse_2F_EventScript_CheckRivalsPC::
	msgbox gText_PokemonTrainerSchoolEmail, MSGBOX_DEFAULT
	releaseall
	end

PlayersHouse_2F_EventScript_Notebook::
	msgbox PlayersHouse_2F_Text_Notebook, MSGBOX_SIGN
	end

PlayersHouse_2F_EventScript_GameCube::
	msgbox PlayersHouse_2F_Text_ItsAGameCube, MSGBOX_SIGN
	end

PlayersHouse_2F_Text_ClockIsStopped:
	.string "The clock is stopped…\p"
	.string "Better set it and start it!$"

PlayersHouse_2F_Text_HowDoYouLikeYourRoom:
	.string "MOM: {PLAYER}, how do you like your\n"
	.string "new room?\p"
	.string "Good! Everything's put away neatly!\p"
	.string "They finished moving everything in\n"
	.string "downstairs, too.\p"
	.string "POKéMON movers are so convenient!\p"
	.string "Oh, you should make sure that\n"
	.string "everything's all there on your desk.$"

PlayersHouse_2F_Text_Notebook:
	.string "{PLAYER} flipped open the notebook.\p"
	.string "ADVENTURE RULE NO. 1\n"
	.string "Open the MENU with START.\p"
	.string "ADVENTURE RULE NO. 2\n"
	.string "Record your progress with SAVE.\p"
	.string "The remaining pages are blank…$"

Common_Text_LookCloserAtMap:
	.string "{PLAYER} took a closer look at the\n"
	.string "HOENN region map.$"

PlayersHouse_2F_Text_ItsAGameCube:
	.string "It's a Nintendo GameCube.\p"
	.string "A Game Boy Advance is connected to\n"
	.string "serve as the Controller.$"
`

script MCRoom_EventScript_Starter{
	while (!(flag(FLAG_STARTER_SNIVY) || flag(FLAG_STARTER_TEPIG) || flag(FLAG_STARTER_OSHAWOTT))){
		lockall
		msgbox("{PLAYER} opened the bag!")
		message("Which starter are you choosing?")
		waitmessage
		multichoice(20, 5, MULTI_STARTER_CHOICE, 0)
		closemessage
		if (var(VAR_RESULT) == 0){
			call(StarterFade)
			call(MCRoom_EventScript_Snivy)
		}
		elif (var(VAR_RESULT) == 1){
			call(StarterFade)
			call(MCRoom_EventScript_Tepig)
		}
		elif (var(VAR_RESULT) == 2){
			call(StarterFade)
			call(MCRoom_EventScript_Oshawott)
		}
		else {
			msgbox("Cheren: Go on. We are waiting...")
			waitmessage
		}
	}
	// else {
	// 	msgbox("The bag is empty.", MSGBOX_DEFAULT)
	// }
	setvar(VAR_LITTLEROOT_START, 3)
	call(MCRoom_EventScript_BattleBianca)
	releaseall
	end
}

script StarterFade {
	fadescreen(FADE_TO_BLACK)
	delay(20)
	fadescreen(FADE_FROM_BLACK)
	return
}

script MCRoom_EventScript_Snivy{
	
	showmonpic(SPECIES_SNIVY, 10, 3)
	msgbox("Are you sure?", MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES){
		givemon(SPECIES_SNIVY, 5, ITEM_ORAN_BERRY)
		if (var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
			playfanfare(MUS_OBTAIN_ITEM)
			msgbox("Received Snivy")
			removeobject(0)
			setflag(FLAG_STARTER_SNIVY)
            setflag(FLAG_SYS_POKEMON_GET)
			setvar(VAR_STARTER_MON, 0)
			call(NicknamePartyMon)
		}
		elif (var(VAR_RESULT) == MON_GIVEN_TO_PC){
			playfanfare(MUS_OBTAIN_ITEM)
			msgbox("Received Snivy")
			removeobject(0)
			setflag(FLAG_STARTER_SNIVY)
            setflag(FLAG_SYS_POKEMON_GET)
			call(NicknameBoxMon)
		}
		else {
			hidemonpic
			msgbox("You have no more room for this Pokemon.")
		}
	}
	else {
		hidemonpic
		msgbox("You did not choose the Pokemon")
	}
	waitmessage
	return
}

script MCRoom_EventScript_Tepig{
	
	showmonpic(SPECIES_TEPIG, 10, 3)
	msgbox("Are you sure?", MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES){
		givemon(SPECIES_TEPIG, 5, ITEM_ORAN_BERRY)
		if (var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
			playfanfare(MUS_OBTAIN_ITEM)
			msgbox("Received Tepig")
			removeobject(1)
			setflag(FLAG_STARTER_TEPIG)
            setflag(FLAG_SYS_POKEMON_GET)
			setvar(VAR_STARTER_MON, 1)
			call(NicknamePartyMon)
		}
		elif (var(VAR_RESULT) == MON_GIVEN_TO_PC){
			playfanfare(MUS_OBTAIN_ITEM)
			msgbox("Received Tepig")
			removeobject(1)
			setflag(FLAG_STARTER_TEPIG)
            setflag(FLAG_SYS_POKEMON_GET)
			call(NicknameBoxMon)
		}
		else {
			hidemonpic
			msgbox("You have no more room for this Pokemon.")
		}
	}
	else {
		hidemonpic
		msgbox("You did not choose the Pokemon")
	}
	waitmessage
	return
}

script MCRoom_EventScript_Oshawott{

	showmonpic(SPECIES_OSHAWOTT, 10, 3)
	msgbox("Are you sure?", MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES){
		givemon(SPECIES_OSHAWOTT, 5, ITEM_ORAN_BERRY)
		if (var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
			playfanfare(MUS_OBTAIN_ITEM)
			msgbox("Received Oshawott")
			removeobject(2)
			setflag(FLAG_STARTER_OSHAWOTT)
            setflag(FLAG_SYS_POKEMON_GET)
			setvar(VAR_STARTER_MON, 2)
			call(NicknamePartyMon)
		}
		elif (var(VAR_RESULT) == MON_GIVEN_TO_PC){
			playfanfare(MUS_OBTAIN_ITEM)
			msgbox("Received Oshawott")
			removeobject(2)
			setflag(FLAG_STARTER_OSHAWOTT)
            setflag(FLAG_SYS_POKEMON_GET)
			call(NicknameBoxMon)
		}
		else {
			hidemonpic
			msgbox("You have no more room for this Pokemon.")
		}
	}
	else {
		hidemonpic
		msgbox("You did not choose the Pokemon")
	}
	waitmessage
	return
}

script NicknamePartyMon{ # Revisar que se esté ejecutando correctamente
	
	msgbox("Do you want to give it a nickname?", MSGBOX_YESNO)
	waitbuttonpress
	hidemonpic
	if (var(VAR_RESULT) == YES){
		call(Common_EventScript_GetGiftMonPartySlot)
		call(Common_EventScript_NameReceivedPartyMon)
	}
}

script NicknameBoxMon{

    msgbox("Do you want to give it a nickname?", MSGBOX_YESNO)
	waitbuttonpress
    hidemonpic
    if (var(VAR_RESULT) == YES){
        call(Common_EventScript_NameReceivedBoxMon)
    }
    call(Common_EventScript_TransferredToPC)
}

script MCRoom_EventScript_StarterBag{
	if (!(flag(FLAG_STARTER_SNIVY) || flag(FLAG_STARTER_TEPIG) || flag(FLAG_STARTER_OSHAWOTT))){
		setvar(VAR_LITTLEROOT_START, 1)
		clearflag(FLAG_STARTER_BAG)
	}
	else {
		setflag(FLAG_STARTER_BAG)
	}
}

script MCRoom_EventScript_DoNotLeaveBeggining{
	lockall
	msgbox("Cheren: Come here {PLAYER}!$")
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left))
	waitmovement(0)
	releaseall
}

script MCRoom_EventScript_Beggining{
	lockall
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_0) == 7){
		applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left))
	}
	else {
		applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down))
	}
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Movement_MCStart1)
	waitmovement(0)
	applymovement(LOCALID_MC2F_CHEREN, moves(face_left))
	msgbox("Cheren: {PLAYER}!\pI heard from Professor Juniper.\nWe can have a Pokémon?$")
	waitmessage
	applymovement(LOCALID_MC2F_CHEREN, moves(face_down))
	waitmovement(0)
	msgbox("...\n...$")
	msgbox("Cheren: What's keeping Bianca?$")
	waitmessage
	closemessage
	delay(90)
	clearflag(FLAG_MC2F_BIANCA)
	showobjectat(LOCALID_MC2F_BIANCA, MAP_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F)
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_diag_northeast * 2, walk_right))
	waitmovement(0)
	opendoor(8, 1)
	waitdooranim
	delay(60)
	applymovement(LOCALID_MC2F_BIANCA, moves(walk_down))
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
	applymovement(LOCALID_MC2F_CHEREN, moves(face_up))
	waitmovement(0)
	msgbox("Bianca: Am I a little late again?\nSo-oooo-orry!$")
	waitmessage
	applymovement(LOCALID_MC2F_CHEREN, moves(walk_right, walk_up))
	waitmovement(0)
	msgbox("Cheren: Bianca...\lI've known for ten years that you\nhave no sense of time, but...seriously...\pToday's 
			the day we can get a\nPokémon from Professor Juniper!$")
	waitmessage
	applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_diag_southwest * 2, walk_left))
	applymovement(LOCALID_MC2F_BIANCA, moves(walk_down, walk_left * 4, face_down))
	waitmovement(0)
	applymovement(LOCALID_MC2F_CHEREN, moves(face_left))
	waitmovement(0)
	msgbox("Bianca: I know. Sorry, {PLAYER}.\nSorry, Cheren.$")
	waitmessage
	applymovement(LOCALID_MC2F_BIANCA, moves(walk_left, walk_down * 2, face_right))
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
	waitmovement(0)
	msgbox("Bianca: So, where are the Pokémon?\pThey were delivered to {PLAYER}'s\nhouse, so {PLAYER} gets first pick.$")
	msgbox("Cheren: Naturally.$")
	waitmessage
	applymovement(LOCALID_MC2F_CHEREN, moves(walk_down, walk_left, face_down))
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
	applymovement(LOCALID_MC2F_BIANCA, moves(face_down))
	waitmovement(0)
	msgbox("The Pokémon are waiting for us\ninside that bag.\pOK, {PLAYER}, you go first and take\na peek at the bag.\p
			I want to meet the Pokémon right now!$")
	waitmessage
	
	special(RemoveCameraObject)
	setvar(VAR_LITTLEROOT_START, 2)
	releaseall
}

movement Movement_MCStart1{
	walk_left * 2
	walk_down * 2
}

script MCRoom_EventScript_ChooseNow{
	lockall
	msgbox("Hey, don't leave.\nWe're waiting.$")
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down))
	waitmovement(0)
	releaseall
}

script MCRoom_EventScript_BattleBianca{
	lockall
	// After choosing starter
	applymovement(LOCALID_MC2F_BIANCA, moves(walk_down, face_right))
	waitmovement(0)
	msgbox("Bianca: OK, I'll take this Pokémon!\nCheren, that one's yours!$")
	applymovement(LOCALID_MC2F_CHEREN, moves(walk_down, face_left))
	waitmovement(0)
	if (var(VAR_STARTER_MON) == 0){ # Snivy
		msgbox("Cheren: Hey, how come you get to pick\nout my Pokémon?\pOh, never mind. I wanted Tepig\nfrom the start, 
				anyway.$")
	}
	elif (var(VAR_STARTER_MON) == 1){ # Tepig
		msgbox("Cheren: Hey, how come you get to pick\nout my Pokémon?\pOh, never mind. I wanted Oshawott\nfrom the start, 
				anyway.$")
	}
	elif (var(VAR_STARTER_MON) == 2){ # Oshawott
		msgbox("Cheren: Hey, how come you get to pick\nout my Pokémon?\pOh, never mind. I wanted Snivy\nfrom the start, 
				anyway.$")
	}
	waitmessage
	fadescreen(FADE_TO_BLACK)
	removeobject(15)
	applymovement(LOCALID_MC2F_CHEREN, moves(walk_fast_up * 4, face_down))
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_fast_up * 2, face_right))
	applymovement(LOCALID_MC2F_BIANCA, moves(walk_fast_right * 3, walk_fast_up * 2, face_left))
	waitmovement(0)
	fadescreen(FADE_FROM_BLACK)
	msgbox("Bianca: Everyone has chosen a Pokémon!\n...So that's that!\pHey, I know!\nLet's have a Pokémon battle!$")
	msgbox("Cheren: Honestly, Bianca...\pEven though they're still weak Pokémon,\nyou shouldn't have Pokémon battles\l
			inside a house.\pDon't be a worrywart!\nThese little ones are weak, like you said.\pWe have to let them 
			battle so they'll get\nstronger. It's settled, {PLAYER}!\lGet ready for a Pokémon Battle!$")
	waitmessage
	applymovement(LOCALID_MC2F_BIANCA, Common_Movement_Delay48)
	waitmovement(0)
	// Battle with Bianca
	switch (var(VAR_STARTER_MON)){
		case 0: 
			call(MCRoom_EventScript_StartBiancaBattleSnivy)
		case 1: 
			call(MCRoom_EventScript_StartBiancaBattleTepig)
		case 2: 
			call(MCRoom_EventScript_StartBiancaBattleOshawott)
	}
	releaseall
	end
}
script MCRoom_EventScript_BattleCheren{
	lockall
	msgbox("Bianca: Wow...\p{PLAYER}, you're gonna be\nan awesome Trainer someday, I can tell!\pNo doubt!$")
	waitmessage
	msgbox("Cheren: Uh...\n...$")
	msgbox("Bianca: Would you take a look around?$")
	waitmessage
	applymovement(LOCALID_MC2F_BIANCA, moves(face_right))
	waitmovement(0)
	delay(60)
	applymovement(LOCALID_MC2F_BIANCA, moves(face_up))
	waitmovement(0)
	delay(60)
	applymovement(LOCALID_MC2F_BIANCA, moves(face_down))
	waitmovement(0)
	delay(60)
	msgbox("Bianca: Wh-whoa!\nWhat happened?!$")
	applymovement(LOCALID_MC2F_BIANCA, moves(face_left))
	waitmovement(0)
	msgbox("Wow! Pokémon are amaaaaazing!!\nSo little, but so strong!\pI'm so glad that I get to have a Pokémon!\p
			...Oh.\nUm, sorry about your room, {PLAYER}.$")
	waitmessage
	applymovement(LOCALID_MC2F_CHEREN, moves(walk_down * 2, face_right))
	waitmovement(0)
	msgbox("Cheren: You...are completely hopeless.\pHere!\nI'll restore your Pokémon for you.$")
	// Heals Bianca's Pokémon
	playfanfare(MUS_HEAL)
	waitfanfare
	applymovement(LOCALID_MC2F_CHEREN, moves(face_left))
	waitmovement(0)
	msgbox("{PLAYER}'s Pokémon needs\nto be fixed up, too.$")
	// Heals {PLAYER}'s Pokémon
	playfanfare(MUS_HEAL)
	waitfanfare
	special(HealPlayerParty)
	// Exclamation sign in Bianca
	applymovement(LOCALID_MC2F_BIANCA, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(LOCALID_MC2F_BIANCA, moves(walk_up * 2, walk_left, face_down))
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
	applymovement(LOCALID_MC2F_CHEREN, moves(face_up))
	waitmovement(0)
	msgbox("Bianca: Hey, Cheren!\nHow about you battle, too?\pWith all you know, I'm sure you can\nbattle without 
			turning the room into\la disaster area like I did!$")
	msgbox("Cheren: I believe you're right!\pIt'll be no problem for me to keep\nthe room from getting any mesier.\p
			Besides, it's not fair if you two are the\nonly ones who get to have fun battling!$")
	waitmessage
	applymovement(LOCALID_MC2F_CHEREN, moves(walk_right, face_left))
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
	waitmovement(0)
	msgbox("It's decided. You'll be my opponent...\nin our first Pokémon battle!\pLet's see what you can do, ...!$")
	waitmessage
	applymovement(LOCALID_MC2F_CHEREN, Common_Movement_Delay48)
	waitmovement(0)
	// Battle with cheren
	switch (var(VAR_STARTER_MON)){
		case 0: call(MCRoom_EventScript_StartCherenBattleSnivy)
		case 1: call(MCRoom_EventScript_StartCherenBattleTepig)
		case 2: call(MCRoom_EventScript_StartCherenBattleOshawott)
	}
	releaseall
	end
}
script MCRoom_EventScript_PostBattles{
	lockall
	msgbox("Cheren: I made a strange blunder\nin my first battle, but\lthis feeling I have...\l...I'm finally a Trainer
			.\pBut first, we'd better go apologize to\nyour mother about this messed-up room.$")
	waitmessage
	applymovement(LOCALID_MC2F_CHEREN, moves(walk_right * 2, walk_up * 3))
	msgbox("Bianca: Oh!\nI'd better come, too!$")
	waitmessage
	removeobject(LOCALID_MC2F_CHEREN)
	applymovement(LOCALID_MC2F_BIANCA, moves(walk_right * 3, walk_up))
	waitmovement(0)
	removeobject(LOCALID_MC2F_BIANCA)

	setflag(FLAG_MC2F_BIANCA)
	setflag(FLAG_MC2F_CHEREN)
	releaseall
	end
}

script MCRoom_EventScript_StartBiancaBattleSnivy{
	trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_TREECKO, MCRoom_Text_BiancaDefeated)
	call(MCRoom_EventScript_BattleCheren)
	end
}
script MCRoom_EventScript_StartBiancaBattleTepig{
	trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_TORCHIC, MCRoom_Text_BiancaDefeated)
	call(MCRoom_EventScript_BattleCheren)
	end
}
script MCRoom_EventScript_StartBiancaBattleOshawott{
	trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_MUDKIP, MCRoom_Text_BiancaDefeated)
	call(MCRoom_EventScript_BattleCheren)
	end
}

text MCRoom_Text_BiancaDefeated{
	"The Pokémon on both sides did their best!"
}

script MCRoom_EventScript_StartCherenBattleSnivy{
	trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_TREECKO, MCRoom_Text_CherenDefeated)
	call(MCRoom_EventScript_PostBattles)
	end
}
script MCRoom_EventScript_StartCherenBattleTepig{
	trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_TORCHIC, MCRoom_Text_CherenDefeated)
	call(MCRoom_EventScript_PostBattles)
	end
}
script MCRoom_EventScript_StartCherenBattleOshawott{
	trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_MUDKIP, MCRoom_Text_CherenDefeated)
	call(MCRoom_EventScript_PostBattles)
	end
}

text MCRoom_Text_CherenDefeated{
	"Ah! So that's a Pokémon battle!"
}